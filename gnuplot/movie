if (!gif) load d.'/gnuplot/_anim'
if (gif) load d.'/gnuplot/_gif'

set size ratio -1 # scale 1:1

set nokey
set nomouse
set macros

r = 1.2

set xrange [-r:r]
set yrange [-r:r]

max_eval_time = 100
resolution = 0.01

t = 0
T = max_eval_time * (1 / resolution)
dt = 1
pauseTime = 0

# 'every 1::t::t' plots only the 't'th point.
# 'every 10::0::t' plots every 10th point from 0 to the 't'th point.

point = "every 1::t::t u (sin($2)):(cos($2)) w p"
bar = "every 1::t::t u (0):(0):(sin($2)):(cos($2)) w vectors"
torque = "every 1::t::t u (sin($2)):(cos($2)):($4/20*cos($2)):(-$4/20*sin($2)) w vectors"

set style line 1 ps 3 pt 7         # pendulum ball
set style arrow 1 nohead ls 1      # pendulum bar
set style arrow 2 nohead ls 3 lw 3 # torque

plot 'pendulum' @point  ls 1, \
     ''         @bar    as 1, \
     ''         @torque as 2

load d.'/gnuplot/_timestep'
